buildscript {
    dependencies {
        classpath "io.skippy:skippy-gradle:${file('../version.txt').text.trim()}"
    }
}

plugins {
    id 'java'
}

apply plugin: io.skippy.gradle.SkippyPlugin;

skippy {
    sourceSet {
        name = 'test'
        testTask = 'test'
    }
    sourceSet {
        name = 'intTest'
        testTask = 'integrationTest'
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'

    testImplementation project(':skippy-junit5')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'

    intTestImplementation project(':skippy-junit5')
    intTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    intTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'

}

test {
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams true
    }
    useJUnitPlatform()
}


configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    intTestImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    intTestRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test

    useJUnitPlatform()

    testLogging {
        events "passed"
    }
}

check.dependsOn integrationTest